plugins {
    id 'com.android.application'
}

//获取时间，添加到发布版本中
static def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

enum KeyType {
    Integer,
    String,
    Boolean
}

static def getTargetType(KeyType keyType, String str) {
    def value
    switch (keyType) {
        case KeyType.Integer:
            value = Integer.valueOf(str)
            break
        case KeyType.String:
            value = str
            break
        case KeyType.Boolean:
            value = Boolean.valueOf(str)
            break
    }

    return value
}

def getVersionInfo(String releaseKey, String debugKey, KeyType keyType) {
    def versionFile = file('version.properties')

    if (versionFile.canRead()) {
        Properties properties = new Properties()
        properties.load(new FileInputStream(versionFile))
        //读取version.properties文件存放的版本号。
        def versionInfo = getTargetType(keyType, properties[releaseKey])

        def taskNames = gradle.startParameter.taskNames
        for (def name : taskNames) {
            if (name.toString().toLowerCase().contains('release')) {
                versionInfo = getTargetType(keyType, properties[releaseKey])
            } else if (name.toString().toLowerCase().contains('debug')) {
                versionInfo = getTargetType(keyType, properties[debugKey])
            }
        }

        return versionInfo
    } else {
        throw new GradleException("无法读取version.properties文件！")
    }
}

// 生成版本信息
def generateVersionCode() {
    return getVersionInfo("STABLE_VERSION_CODE", "ALPHA_VERSION_CODE", KeyType.Integer)
}

def generateVersionName() {
    return getVersionInfo("VERSION_NAME", "VERSION_NAME", KeyType.String)
}

// 是否启用精简
static def isEnableMiniFy() {
    return true
}

static def isShrinkResources() {
    return isEnableMiniFy()
}

// 后缀
def generateSuffix() {
    return getVersionInfo("STABLE_SUFFIX", "ALPHA_SUFFIX", KeyType.String)
}

android {
    namespace 'com.venus.backgroundopt'
    compileSdk 33

    defaultConfig {
        applicationId "com.venus.backgroundopt"
        minSdk 30
        targetSdk 33
        versionCode generateVersionCode()
        versionName generateVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

//        multiDexEnabled true
//        ndk{
//            //强制使用架构 就可以解决打包大问题。
//            abiFilters "arm64-v8a", "armeabi-v7a", "armeabi", "x86_64", "x86"
//        }
    }

    buildTypes {
        release {
            versionNameSuffix generateSuffix()

            minifyEnabled isEnableMiniFy()
            shrinkResources isShrinkResources()
            zipAlignEnabled true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-log.pro'
        }

        debug {
            versionNameSuffix generateSuffix()

            minifyEnabled isEnableMiniFy()
            shrinkResources isShrinkResources()
            zipAlignEnabled true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro', 'proguard-log.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
//    packagingOptions {
//        jniLibs {
//            useLegacyPackaging true
//        }
//    }
    android.applicationVariants.all {
        variant ->
            variant.outputs.all {
                //这里修改apk文件名
                outputFileName = "backgroundOpt_v${defaultConfig.versionName}(${defaultConfig.versionCode})" + generateSuffix() + "_" + releaseTime() + ".apk"
            }
    }
}

dependencies {
//    compileOnly files('libs/XposedBridgeAPI-89.jar')
    compileOnly 'de.robv.android.xposed:api:82'
//    compileOnly 'de.robv.android.xposed:api:82:sources'

//    implementation 'org.slf4j:slf4j-api:1.7.30'
//    implementation 'ch.qos.logback:logback-classic:1.1.3'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}